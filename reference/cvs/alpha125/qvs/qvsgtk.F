      LOGICAL FUNCTION QVSGTK(ITK,VTX,
     >  MNTP,CTMX,D0MX,Z0MX,RVMX,PMIN,CPDMX)
CKEY  QVSRCH / INTERNAL
C ------------------------------------------------------------------------------
C! Applies track cuts including ALPHA lock function
C     QVSGTK IS A FUNCTION NOT A SUBROUTINE
C  Author : T. MATTISON  U.A.BARCELONA/SLAC  1 DECEMBER 1992
C
C  Input Arguments :
C  *  ITK IS ALPHA TRACK NUMBER
C  *  VTX() IS PRIMARY VERTEX COORDINATES FOR CUTS
C  *  MNTP IS MINIMUM TPC HITS
C  *  CTMX IS MAXIMUM COS(THETA)
C  *  D0MX IS MAXIMUM D0 VALUE
C  *  Z0MX IS MAXIMUM Z0 VALUE
C        BOTH D0 AND Z0 ARE CORRECTED FROM ALEPH COORDINATES
C        TO COORDINATES CENTERED ON VTX()
C  *  RVMX IS MAXIMUM 3-D DISTANCE FROM TRACK TO VTX
C        IF CUT IS LESS THAN 3 TIMES Z0 ERROR TRANSVERSE TO TRACK,
C        THE CUT IS APPLIED TO THE 2D RADIAL (D0) DISTANCE TO VTX
C  *  PMIN IS MINIMUM TOTAL MOMENTUM
C  *  CPDMX IS MAXIMUM CHISQUARE/DOF
C  Output Arguments :
C     RETURNS QVSGTK=.TRUE. OR .FALSE.
C
C ------------------------------------------------------------------------------
#ifndef DOC
      DIMENSION VTX(3)
#include "qcde.h"
#include "qmacro.h"
C ------------------------------------------------------------------------------
C
      QVSGTK=.FALSE.
C
C ALPHA LOCK
      IF (XLOCK(ITK)) RETURN
C
C TPC HITS CUT
      IF (KFRTNT(ITK) .LT. MNTP) RETURN
C
C COS-THETA CUT
      IF (ABS(QCT(ITK)) .GT. CTMX) RETURN
C
C FIND DISTANCE FROM VERTEX
      CALL QVSMIS(ITK,VTX,D0V,Z0V,R3D)
C
C D0 CUT (RELATIVE TO VTX)
      IF (ABS(D0V) .GT. D0MX) RETURN
C
C Z0 CUT RELATIVE TO VERTEX
      IF (ABS(Z0V) .GT. Z0MX) RETURN
C
C Z0 ERROR TRANSVERSE TO TRACK
      DZT=SQRT( ABS( QFRFEM(ITK,5,5)/(1.+QFRFTL(ITK)**2) ))
C
C COMPARE 3D DISTANCE CUT TO Z ERROR
      IF (RVMX .LT. 3.*DZT) THEN
C NOT SAFE TO USE 3D DISTANCE
C USE D0 RELATIVE TO VERTEX
        RV=ABS(D0V)
      ELSE
C USE 3D MISS DISTANCE
        RV=R3D
      ENDIF
C
C 3D (OR 2D) DISTANCE FROM VERTEX CUT
      IF (RV .GT. RVMX) RETURN
C
C TOTAL MOMENTUM CUT
      P=QP(ITK)
      IF (P .LT. PMIN) RETURN
C
C CHI SQUARE PER DEGREE OF FREEDOM
      IF (QFRFC2(ITK)/MAX(KFRFDF(ITK),1) .GT. CPDMX) RETURN
C
C TRACK MUST BE OK
      QVSGTK=.TRUE.
C
      RETURN
      END
#endif
