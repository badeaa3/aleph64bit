      FUNCTION NTHX(X,NDIM,N)
C
C     DETERMINE INDEX OF N'TH ELEMENT OF X(1)...X(NDIM)
C
C     FOR N LESS EQUAL 1 THE INDEX OF THE LOWEST ELEMENT, FOR N GREATER
C     EQUAL NDIM THE INDEX OF THE HIGHEST ELEMENT IS RETURNED
C
      REAL X(NDIM)
      NTHX=1
C
C     FIND LOWEST ELEMENT
C
      IF(N.GT.1) GOTO 20
      DO 10 I=1,NDIM
   10 IF(X(I).LT.X(NTHX)) NTHX=I
      GOTO 100
C
C     FIND HIGHEST ELEMENT
C
   20 IF(N.LT.NDIM) GOTO 40
      DO 30 I=1,NDIM
   30 IF(X(I).GT.X(NTHX)) NTHX=I
      GOTO 100
C
C     INITIALIZE SEARCH, USE QUASI RANDOM LOOP FOR EFFICIENCY IN CASE OF
C     SORTED X-ARRAY
C
   40 LIMLOW=0
      LIMHIG=0
      KPLUS=SQRT(FLOAT(NDIM))+0.3
      K=1-KPLUS
      DO 80 L=1,NDIM
      K=K+KPLUS
      IF(K.GT.NDIM) K=MOD(K,KPLUS)+1
C
C     TEST K'TH ELEMENT AGAINST LIMITS
C
      IF(LIMLOW.EQ.1.AND.X(K).LE.XLOW) GOTO 80
      IF(LIMHIG.EQ.1.AND.X(K).GE.XHIG) GOTO 80
      NLOW=0
      NHIG=0
      DO 50 I=1,NDIM
      IF(X(I)-X(K)) 42,50,44
   42 NLOW=NLOW+1
      GOTO 50
   44 NHIG=NHIG+1
   50 CONTINUE
C
C     UPDATE LOWER LIMIT
C
      IF(NLOW.GE.N) GOTO 60
      LIMLOW=1
      XLOW=X(K)
C
C     UPDATE HIGHER LIMIT
C
   60 IF(NHIG.GT.NDIM-N) GOTO 70
      LIMHIG=1
      XHIG=X(K)
C
C     TEST
C
   70 IF(LIMLOW.EQ.1.AND.LIMHIG.EQ.1.AND.
     1 XLOW.EQ.XHIG) GOTO 90
   80 CONTINUE
   90 NTHX=K
  100 RETURN
      END
