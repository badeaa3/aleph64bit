      SUBROUTINE QVSVTX(PVTX,EPVTX,DJET,BTAG,SVTX,ESVTX,IERR)
CKEY  QVSRCH / INTERNAL
C ------------------------------------------------------------------------------
C! Finds secondary vertices, errors, and B-tag variables, for 2 jets
C  Called from QVSRCH
C  Author : T. MATTISON  U.A.BARCELONA/SLAC  1 DECEMBER 1992
C
C  Input Arguments :
C  *  PVTX() IS PRIMARY VERTEX IN ALEPH XYZ COORDINATES
C  *  EPVTX() IS ERROR (NOT VARIANCE) IN PVTX
C  *  DJET(3,2) IS TWO NORMALIZED XYZ DIRECTION VECTORS FOR TWO JETS
C  Output Arguments :
C  *  BTAG(2) IS THE B-TAGGING VARIABLE FOR THE TWO HEMISPHERES
C        IT IS BASICALLY THE HALF THE CHI-SQUARE DIFFERENCE BETWEEN THE
C        HYPOTHESIS THAT ALL TRACKS IN THE HEMISPHERE COME FROM PRIMARY
C        OR THAT SOME TRACKS COME FROM THE SECONDARY AND SOME FROM
C        PRIMARY A CUT AT 10, OR AT 20 ON THE SUM, IS REASONABLE
C  *  SVTX(3,2) IS TWO SECONDARY VERTICES IN COORDINATE SYSTEMS
C        THAT ARE USED INTERNALLY, (ONE SYSTEM PER JET)
C        ORIGINS ARE AT PVTX(), AND ARE ORIENTED BY DJET(,)
C        THE THIRD COORDINATE IS THE DECAY LENGTH IN THE JET DIRECTION
C        THE FIRST COORDINATE IS R-PHI-LIKE, THE SECOND IS Z-LIKE
C  *  ESVTX(3,2) IS THE ERRORS ON SVTX() IN THE INTERNAL ROTATED SYSTEM
C        WHERE GEOMETRICAL CORRELATIONS ARE SMALL AND IGNORED
C        MAY BE NEGATIVE, INDICATING A PROBLEM, NOT NECESSARILY SEVERE,
C        IN FINDING THE VERTEX
C  *  IERR IS ERROR FLAG
C       0 FOR OK
C       1 FOR SECONDARY VERTEX 1 ERRORS QUESTIONABLE
C       2 FOR SECONDARY VERTEX 2 ERRORS QUESTIONABLE
C       3 FOR QUESTIONABLE ERRORS ON BOTH SECONDARY VERTICES
C       4 FOR NO GOOD TRACKS IN JET 1
C       8 FOR NO GOOD TRACKS IN JET 2
C      12 FOR NO GOOD TRACKS AT ALL
C
C ------------------------------------------------------------------------------
#ifndef DOC
      DIMENSION PVTX(3),EPVTX(3),DJET(3,2)
      DIMENSION BTAG(2),SVTX(3,2),ESVTX(3,2)
C LENGTH OF GOOD-TRACK LIST
      PARAMETER (MGTK=100)
      DIMENSION JGTK(MGTK)
C NUMBER OF POINTS IN X SCAN
      PARAMETER (LBX=50)
C NUMBER OF POINTS IN Z SCAN
      PARAMETER (LBZ=50)
C NUMBER OF POINTS IN DECAY-LENGTH SCAN
      PARAMETER (LBT=100)
C SIZE NEEDED FOR TWO DECAY-LENGTH PROJECTIONS
      PARAMETER (LVLF=(LBX+LBZ)*LBT)
C
#include "bcs.h"
      COMMON /YVSWRK/ IPTR
C ------------------------------------------------------------------------------
C
      IERR=0
C
C MAKE GOOD TRACK LIST
      CALL QVSGTL1(PVTX,MGTK,NGTK,JGTK)
C BAIL OUT IF NO TRACKS
      IF (NGTK .LT. 1) THEN
        IERR=12
        RETURN
      ENDIF
C
C GET SOME WORK SPACE
      IPTR=0
      LEN=LVLF
      CALL QVSWBK(LEN,'QVSV')
C
C JET VERTEX CONTROL PARAMETERS
C BINS FOR ROTATED X
      NBX=LBX
C RANGE FOR ROTATED X (+/- 500 MICRONS)
      XL=-.0500
      XH=+.0500
C BINS FOR ROTATED Z
      NBZ=LBZ
C RANGE FOR ROTATED Z (+/- 500 MICRONS)
      ZL=-.0500
      ZH=+.0500
C BINS FOR DISTANCE ALONG JET DIRECTION
      NBT=LBT
C RANGE ALONG JET DIRECTION (+/- 1 CM)
      TL=-1.
      TH=1.
C JET ANGLE RESOLUTION (50 MILLIRADIANS)
      EJET=.050
C CUT OFF TRACKS AT 3 SIGMA
      SGMX=3.
C
C FIND SECONDARY VERTEX FOR FIRST JET
      CALL YVSXZJ(NGTK,JGTK,PVTX,EPVTX,DJET,1,EJET,SGMX,
     > NBX,XL,XH,NBZ,ZL,ZH,NBT,TL,TH,RW(IPTR+1),
     > SVTX(1,1),ESVTX(1,1),SVTX(2,1),ESVTX(2,1),SVTX(3,1),ESVTX(3,1),
     > DLLX1,DLLZ1)
C
      IF (ESVTX(1,1) .EQ. -999.) THEN
C MEAN THERE WERE NO TRACKS IN THE JET
        IERR=IERR+4
      ELSEIF (ESVTX(1,1) .LT. 0. .OR. ESVTX(2,1) .LT. 0.
     >                       .OR. ESVTX(3,1) .LT. 0. ) THEN
C MEANS AN INTERPOLATION PROBLEM
        IERR=IERR+1
      ENDIF
C
C AND SECOND JET
      CALL YVSXZJ(NGTK,JGTK,PVTX,EPVTX,DJET,2,EJET,SGMX,
     > NBX,XL,XH,NBZ,ZL,ZH,NBT,TL,TH,RW(IPTR+1),
     > SVTX(1,2),ESVTX(1,2),SVTX(2,2),ESVTX(2,2),SVTX(3,2),ESVTX(3,2),
     > DLLX2,DLLZ2)
C
      IF (ESVTX(1,2) .EQ. -999.) THEN
C MEAN THERE WERE NO TRACKS IN THE JET
        IERR=IERR+8
      ELSEIF (ESVTX(1,2) .LT. 0. .OR. ESVTX(2,2) .LT. 0.
     >                       .OR. ESVTX(3,2) .LT. 0. ) THEN
C MEANS AN INTERPOLATION PROBLEM
        IERR=IERR+2
      ENDIF
C
C TAG VARIABLES
      BTAG(1)=DLLX1+DLLZ1
      BTAG(2)=DLLX2+DLLZ2
C
  999 CONTINUE
C DROP WORK BANK
      CALL WDROP(IW,IPTR)
C
      RETURN
      END
#endif
