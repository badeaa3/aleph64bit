      SUBROUTINE QVSIMP(ITK,PVTX,DJET,SVTX,EPVTX,ESVTX,
     >   DXP,DZP,DTP,DXS,DZS,DTS)
CKEY  QVSRCH / INTERNAL
C ------------------------------------------------------------------------------
C! Returns normalized signed impact parameters
C     relative to primary, secondary vertices, in x,z,t
C     X IS BASICALLY D0, Z IS BASICALLY Z0, T IS ALONG JET DIRECITON
C  Author : T. MATTISON  U.A.BARCELONA/SLAC  1 DECEMBER 1992
C*    MODIFIED BY T. MATTISON  8 DEC 1992
C*    PROTECT AGAINST NONSENSE ERRORS AND ZERO SLOPES
C
C  Input Arguments :
C  *  ITK IS ALPHA TRACK NUMBER
C  *  PVTX() IS PRIMARY VERTEX IN ALEPH COORDINATES
C  *  DJET() IS NORMALIZED JET DIRECTION IN ALEPH COORDINATES
C  *  SVTX() IS SECONDARY VERTEX IN TRANSLATED-ROTATED COORDINATES
C        ORIGIN AT PVTX, 3D AXIS IN DJET DIRECTION
C  *  EPVTX() IS ERROR IN PRIMARY VERTEX
C  *  ESVTX() IS ERROR IN SECONDARY VERTEX
C        ZERO ERROR IS OK (AND WORKS FAIRLY WELL)
C  Output Arguments :
C  *  DXP IS X NORMALIZED SIGNED IMPACT RELATIVE TO PRIMARY
C  *  DZP IS Z NORMALIZED SIGNED IMPACT RELATIVE TO PRIMARY
C  *  DTP IS COMBINATION OF X AND Z SIGNED IMPACT PARAMETERS AT PRIMARY,
C        AND ERRORS AND SLOPES CORRESPONDING TO
C        NORMALIZED DISTANCE ALONG JET DIRECTION
C  *  DXS IS X NORMALIZED SIGNED IMPACT RELATIVE TO SECONDARY
C  *  DZS IS Z NORMALIZED SIGNED IMPACT RELATIVE TO SECONDARY
C  *  DTS IS LIKE DTP BUT RELATIVE TO SECONDARY
C
C ------------------------------------------------------------------------------
#ifndef DOC
      DIMENSION PVTX(3),DJET(3),SVTX(3)
      DIMENSION EPVTX(3),ESVTX(3)
C ------------------------------------------------------------------------------
C NULL-RESULT VALUES
      DXP=-999.
      DZP=-999.
      DTP=-999.
      DXS=-999.
      DZS=-999.
      DTS=-999.
C
C FIND TRACK PARAMETERS IN VERTEX-JET COORDINATES
      CALL QVSTKP(ITK,PVTX,DJET,
     > XP,DXDT,D2XDT2,ZP,DZDT,D2ZDT2,VXP,VZP,VXZ)
C
C PUNT IF NONSENSE ERRORS
      IF (VXP .LE. 0.) RETURN
      IF (VZP .LE. 0.) RETURN
C
C FIND PRIMARY VERTEX ERRORS IN ROTATED SYSTEM
      CALL YVSPVE(EPVTX,DJET,EX,EZ,ET)
C
C PRIMARY VERTEX ERRORS PROJECTED WITH TRACK SLOPES
      EPX2=EX**2+(DXDT*ET)**2
      EPZ2=EZ**2+(DZDT*ET)**2
C
C NORMALIZED MISS DISTANCES FROM PRIMARY
      DXP=XP/SQRT(VXP+EPX2)
      DZP=ZP/SQRT(VZP+EPZ2)
C
C DISTANCE ALONG JET AXIS FROM PRIMARY
      IF (DXDT .NE. 0. .OR. DZDT .NE. 0.) THEN
        WX=DXDT**2/(VXP+EPX2)
        WZ=DZDT**2/(VZP+EPZ2)
        DTP=(XP*DXDT/(VXP+EPX2)+ZP*DZDT/(VZP+EPZ2))/SQRT(WX+WZ)
      ENDIF
C
C TRACK COORDINATES AT DISTANCE OF SECONDARY
      T=SVTX(3)
      XS=XP+DXDT*T+.5*D2XDT2*T**2
      ZS=ZP+DZDT*T+.5*D2ZDT2*T**2
C
C SUBTRACT OFF SECONDARY
      XS=XS-SVTX(1)
      ZS=ZS-SVTX(2)
C
C SECONDARY VERTEX ERRORS PROJECTED WITH TRACK SLOPES
      ESX2=ESVTX(1)**2+(DXDT*ESVTX(3))**2
      ESZ2=ESVTX(2)**2+(DZDT*ESVTX(3))**2
C
C NORMALIZED MISS DISTANCES FROM SECONDARY
      DXS=XS/SQRT(VXP+ESX2)
      DZS=ZS/SQRT(VZP+ESZ2)
C
C DISTANCE ALONG JET AXIS FROM SECONDARY
      IF (DXDT .NE. 0. .OR. DZDT .NE. 0.) THEN
        WX=DXDT**2/(VXP+ESX2)
        WZ=DZDT**2/(VZP+ESZ2)
        DTS=(XS*DXDT/(VXP+ESX2)+ZS*DZDT/(VZP+ESZ2))/SQRT(WX+WZ)
      ENDIF
C
C DO THE SIGNS (EASY IN JET COORDINATE SYSTEM)
      DXP=SIGN(DXP,-DXDT*XP)
      DZP=SIGN(DZP,-DZDT*ZP)
      DTP=-DTP
      DXS=SIGN(DXS,-DXDT*XS)
      DZS=SIGN(DZS,-DZDT*ZS)
      DTS=-DTS
C
      RETURN
      END
#endif
