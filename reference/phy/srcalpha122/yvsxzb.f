      SUBROUTINE YVSXZB(BPOS,BSIZ,NGTK,JGTK,TSMR,SGMX,
     > NBX,XL,XH,NBZ,ZL,ZH,VLF,
     > XV,EXV,ZV,EZV,VLFM)
CKEY  QVSRCH / INTERNAL
C ----------------------------------------------------------------------
C! Finds X-Z of primary vertex
C     USING SAMPLED MAXIMUM LIKELIHOOD FIT TO X-Z PLANE
C  Author : T. MATTISON  U.A.BARCELONA/SLAC  1 DECEMBER 1992
C
C  Input Arguments :
C  *  BPOS() IS BEAM POSITION X,Y,Z
C      (Y COORDINATE SHOULD BE HIGH QUALITY, I.E., QVYNOM, OR FROM GETBP
C  *  BSIZ() IS LUMINOUS REGION SIZE (1 SIGMA)
C  *  JGTK IS ARRAY CONTAINING A LIST OF NGTK TRACKS
C  *  TSMR IS ADDITIONAL TRACK SMEARING
C  *  SGMX IS NUMBER OF SIGMA ALLOWED TO USE A TRACK
C  *  NBX,XL,XH ARE BINS, LOW AND HIGH LIMITS FOR VERTEX X
C  *  NBZ,ZL,ZH ARE SAME FOR VERTEX Z
C  *  VLF(NBX*NBZ) IS SAMPLED LOG-LIKELIHOOD ARRAY
C  Output Arguments :
C  *  XV,ZV,EXV,EZV ARE X AND Z VERTEX AND ERROR
C       (Y VERTEX IS IMPLICITLY THE Y BEAM POSITION)
C  *  VLF CONTAINS 2-DIMENSION LOG-LIKELIHOOD FUNCTION:
C  *  VLFM IS THE INTERPOLATED VALUE OF LIKELIHOOD FUNCTION
C        AT THE COMBINED PEAK
C
C ----------------------------------------------------------------------
      DIMENSION BPOS(3),BSIZ(3)
      DIMENSION JGTK(*),VLF(*)
C ----------------------------------------------------------------------
C CLEAR LIKELIHOOD FUNCTION
      CALL UZERO(VLF,1,NBX*NBZ)
C
C ENTER BEAM AS PSEUDO-TRACK
C USE 10 SIGMA CUTOFF (BEAM DOES NOT HAVE TAILS)
      SIGB=10.
      CALL YVSLF2(NBX,XL,XH,BPOS(1),BSIZ(1),
     >            NBZ,ZL,ZH,BPOS(3),BSIZ(3),SIGB,VLF)
C
C FIND BIN WIDTHS
      BWX=(XH-XL)/NBX
      BWZ=(ZH-ZL)/NBZ
C LOOP OVER TRACKS
      DO 250 JTK=1,NGTK
        ITK=JGTK(JTK)
C GET BEAM PLANE INTERCEPTS
        CALL QVSXZB(ITK,TSMR,BPOS(2),XB,VXB,ZB,VZB)
C
C LIMIT ERRORS TO BIN SIZES
        ERX=MAX(SQRT(VXB),ABS(BWX))
        ERZ=MAX(SQRT(VZB),ABS(BWZ))
C
C ADD TRACK IMPACT POINT TO LIKELIHOOD FUNCTION
        CALL YVSLF2(NBX,XL,XH,XB,ERX,
     >               NBZ,ZL,ZH,ZB,ERZ,SGMX,VLF)
  250 CONTINUE
C
C FIND PEAK IN THE LIKELIHOOD FUNCTION
C FIT PARABOLOID TO PEAK TO INTERPOLATE AND GET ERRORS
      CALL YVSML2(NBX,XL,XH,NBZ,ZL,ZH,VLF,
     > XV,EXV,ZV,EZV,XZCR,VLFM)
C
      RETURN
      END
