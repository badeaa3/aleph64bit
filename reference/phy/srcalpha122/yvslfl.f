      SUBROUTINE YVSLFL(NBU,UL,UH,U,EU,EUMX,NBV,VL,VH,DUDV,D2UDV2,
     > VLF)
CKEY  QVSRCH / INTERNAL
C ----------------------------------------------------------------------
C! Adds line (track) to 2-dimension log-likelihood distribution vlf.
C  Author : T. MATTISON  U.A.BARCELONA/SLAC  1 DECEMBER 1992
C
C  Input Arguments :
C  *  NBU,UL,UH ARE NUMBER OF BINS, LOW AND HIGH LIMITS
C        FOR DEPENDENT VARIABLE U
C  *  U,EU ARE VALUE AND SIGMA AT INDEPENDENT VARIABLE V=0
C  *  EUMX IS HOW MANY SIGMAS THE DISTRIBUTION REMAINS GAUSSIAN
C  *  NBV,VL,VH ARE NUMBER OF BINS, LOW AND HIGH LIMITS
C        FOR INDEPENDENT VARIABLE
C  *  DUDV,D2UDV2 ARE 1,2 DERIVATIVES OF U WITH RESPECT TO V
C        U(V)= U + DUDV*V + .5*D2UDV2*V*V
C  *  VLF(NBU*NBV) IS ARRAY CONTAINING LOG-LIKELIHOOD FUNCTION
C        STORED AS VLF(IU,IV)
C  Output Argument :
C     ALTERS VLF()
C
C ----------------------------------------------------------------------
      DIMENSION VLF(*)
C ----------------------------------------------------------------------
C FIND BIN WIDTHS
      BWU=(UH-UL)/NBU
      BWV=(VH-VL)/NBV
C FIND LIMITING LOG-LIKELIHOOD VALUE
      UMX=-.5*EUMX**2
C INVERSE ERROR**2
      REU=1./EU
C CLEAR BIN COUNTER
      IB=0
C LOOP OVER V BINS
      BCV=VL-.5*BWV
      DO 350 IBV=1,NBV
        BCV=BCV+BWV
C FIND POSITION AT THIS V
        UV=U+DUDV*BCV+.5*D2UDV2*BCV**2
C LOOP OVER U BINS
        BCU=UL-.5*BWU
C QUANTITIES FOR FAST-PARABOLA CALCULATION
        T1=-.5*REU**2*BWU*(BWU-2.*UV)
        T2=-.5*REU**2*BWU*2.*BCU
        DT2=-.5*REU**2*BWU*2.*BWU
        FGSN=-.5*((UV-BCU)*REU)**2
        DO 250 IBU=1,NBU
C FIND GAUSSIAN LOG-LIKELIHOOD
C   BCU=BCU+BWU
C   GSN=-.5*((UV-BCU)*REU)**2
C   FASTER EQUIVALENT
          FGSN=FGSN+T1+T2
          T2=T2+DT2
C INCREMENT BIN COUNTER
          IB=IB+1
C INCREMENT WITH LARGER OF V=0 AND PRESENT V BIN
          VLF(IB)=VLF(IB)+MAX(FGSN,UMX)
  250   CONTINUE
  350 CONTINUE
      RETURN
      END
