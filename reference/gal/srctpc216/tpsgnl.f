      SUBROUTINE TPSGNL(IWIR,NLPHIT,NTPHIT,IERR)
C--------------------------------------------------------------------
C!  Subroutine to store signals as they are generated by the avalanche
C!  on the wires and the subsequent coupling to the pads and trigger
C!  pads.
C
C  Called from:  TSTEER
C  Calls:        TPOPSG
C
C  Inputs:   PASSED:      --IWIR,   the number of the wire hit
C                         --NLPHIT, the number of long pads hit
C                         --NTPHIT, the number of trigger pads hit
C            /CHANNL/     --the avalanche size, lists of pads and
C                           trigger pads hit, and lists of signal
C                           sizes on the pads hit
C            /TPCONS/     --NTBNAS, the number of time bins in each
C                                   block of the analog signal bank
C                         --NTSCAN, the number of blocks in the full
C                                   collection time
C
C  Outputs:  BANKS:       --id INDSIG, analog signal bank, opened
C                                      in TPOPSG and filled with
C                                      signals from CHANNL.INC here
C                         --id INDREF, signal reference bank, opened in
C                                      TPINRF and filled with pointers
C                                      and time limits here
C            /TPCBOS/     --NTSGHT, the number of blocks actually used
C                                   in the signal bank
C  D. DeMille
C  M. Mermikides 31/8/86   Check also on INDSIG for opening signal bank
C  P. Janot 03/03/89   Update local indices after a WBANK call !
C--------------------------------------------------------------------
      INTEGER LMHLEN, LMHCOL, LMHROW
      PARAMETER (LMHLEN=2, LMHCOL=1, LMHROW=2)
C
      COMMON /BCS/   IW(1000)
      INTEGER IW
      REAL RW(1000)
      EQUIVALENCE (RW(1),IW(1))
C
C
C  CHANNL carries the analog signals on all channels due to one
C  wire avalanche
C
C  Wire avalanche information
C  NAVELE     -- The charge from an avalanche
C  IBIN1  -- The first bin to get charge
C
      COMMON / AVALAN / NAVELE,IBIN1
C
C  Long pad coupling information
C  MPPUL   -- The array containing the coupled avalanches
C  NAMP   -- Pad 'name' generating this pulse
C            = (pad row-1)*150 + pad number
C
      PARAMETER (MXPHIT=15)
      COMMON / PADPUL / MPPUL(MXPHIT),NAMP(MXPHIT)
C
C  Trigger pad coupling information
C  MTPUL   -- The array containing the coupled avalanches
C  NAMT   -- Pad 'name' generating this pulse
C            = (tpad row# - 1)*(max # tpads/row) + tpad number
C
      PARAMETER (MXTHIT=2)
      COMMON / TRGPUL / MTPUL(MXTHIT),NAMT(MXTHIT)
C
C  TPCBOS contains parameters for handling BOS banks used in the
C  generation of analog and digitized signals in the TPC
C  NCHAN = number of channel types for analog signals and digitizations
C  at present, NCHAN = 3; 1 = wires, 2 = pads, 3 = trigger pads.
      PARAMETER ( NCHAN = 3 )
C
C  Work bank id's.  INDREF(ich) = index for signal reference bank for
C  channel of type ich; INDSIG = index for signal bank for current
C  channel.  IDCLUS = index for cluster bank
C
      COMMON/WORKID/INDREF(NCHAN),INDSIG,IDCLUS,ITSHAP,ITDSHP,
     *              ITPNOI,ITSNOI,ITPULS,ITMADC,INDBRT,INDHL,INDDI
C
C  Parameters for analog signal work banks:  for each type of channel,
C  include max number of channels, default number of channels in
C  signal bank, and number of channels by which to extend signal bank
C  if it becomes full; also keep counter for number of blocks actually
C  filled in signal bank
C
      COMMON/ANLWRK/MAXNCH(NCHAN),NDEFCH,NEXTCH,NTSGHT
C
C  Parameters for digitises (TPP) output banks
C
      COMMON/DIGBNK/NDIDEF(3),NDIEXT(3)
C
C  Hit list and digitization bank parameters: for each type of channel
C  include name nam, default length ndd, and length of extension nde.
C
      COMMON/TPBNAM/DIGNAM(2*NCHAN)
      CHARACTER*4 DIGNAM
C  Name index for track element bank
      COMMON/TPNAMI/NATPTE
C
C
C  TPCONS contains physical constants for TPC simulation
C
      COMMON /DELTA/ CDELTA,DEMIN,DEMAX,DELCLU,RADFAC,CYLFAC
      PARAMETER (MXGAMV = 8)
      COMMON /TGAMM/ GAMVAL(MXGAMV),GAMLOG(MXGAMV),POIFAC(MXGAMV),
     &               POIMAX,POIMIN,CFERMI,CA,CB,POIRAT,POICON
      PARAMETER (MXBINC = 20)
      COMMON /CLUST/ EBINC(MXBINC),CONCLU,WRKFUN,MXCL,CKNMIN,CFANO,CRUTH
     &              ,POWERC
      COMMON /AVALA/ THETA,ETHETA
      COMMON /TPTIME/ NTMXSH,NTMXNO,NTMXAN,NTMXDI,NTSCAN,NTBNAS,NTBAPD
      COMMON /TPELEC/ TPRPAR,TPRSER,TPCFET,TCFEED,TMVPEL,TSIGMX,NTPBIT
C
C  Find the section of the signal collection time that the
C  time bin of this hit corresponds to as well as the corresponding bin
C  number within the section
C
      MSGSC = 1 + ( IBIN1 - 1 ) / NTBNAS
      MBIN = IBIN1 - (MSGSC-1)*NTBNAS
      IERR = 0
C
C  Loop over channel types
C
      DO 999 KCHAN = 1, NCHAN
C
C  See how many channels of this type have been hit
C
         IF ( KCHAN .EQ. 1 ) THEN
            NCHHIT = 1
         ELSEIF ( KCHAN .EQ. 2 ) THEN
            NCHHIT = NLPHIT
         ELSE
            NCHHIT = NTPHIT
         ENDIF
C
C  Loop over the number of channels of this type hit
C
         DO 1 JHIT = 1, NCHHIT
C
C  Find the channel number corresponding to this hit
C
            IF ( KCHAN .EQ. 1 ) THEN
               ICHAN = IWIR
            ELSEIF ( KCHAN .EQ. 2 ) THEN
               ICHAN = NAMP(JHIT)
            ELSE
               ICHAN = NAMT(JHIT)
            ENDIF
C
C  Find the index in the reference bank for the info on this channel
C  idchn = bank index + header length + no. words/chan * (ichan-1)
C
          IDCHN = INDREF(KCHAN) + 2 + (ICHAN-1)*IW(INDREF(KCHAN)+2)
C
C  Check to see if this channel has been hit before in this
C  section of the signal collection time.
C
            IF ( IW(IDCHN+2+MSGSC) .NE. 0 ) THEN
C
C  This channel has been hit before, so just get the pointer
C
               NPNT = IW(IDCHN+2+MSGSC)
C
            ELSE
C
C  This channel not hit before.  Update the no. of channels of this type
C  hit, then get and set the pointer
C
               NTSGHT = NTSGHT + 1
               NPNT = NTSGHT
               IW(IDCHN+2+MSGSC) = NPNT
C
C  If we don't have enough room for this new signal, open or extend the
C  signal bank.  The -1 in the call says we want automatic size control.
C
               IF (INDSIG.EQ.0 .OR. NPNT .GT. IW(INDSIG+2) )
     &            CALL TPOPSG(-1,IERR)
C
               IF (IERR.EQ.1) RETURN
C
            ENDIF
C
C  Reset min and max time bins if necessary
C
            IDCHN = INDREF(KCHAN) + 2 + (ICHAN-1)*IW(INDREF(KCHAN)+2)
            IF ( IBIN1 .LT. IW(IDCHN+1) .OR. IW(IDCHN+1) .EQ. 0 )
     *         IW(IDCHN+1) = IBIN1
C
            IF ( IBIN1 .GT. IW(IDCHN+2) )
     *         IW(IDCHN+2) = IBIN1
C
C  Find the charge to be deposited
C
            IF ( KCHAN .EQ. 1 ) THEN
               NELEC = NAVELE
            ELSEIF ( KCHAN .EQ. 2 ) THEN
               NELEC = MPPUL(JHIT)
            ELSE
               NELEC = MTPUL(JHIT)
            ENDIF
C
C  Deposit charge in proper channel and time bin
C
            IDSGB = INDSIG + 2 + (NPNT-1)*IW(INDSIG+1) + MBIN
            IW(IDSGB) = IW(IDSGB) + NELEC
C
C  End loop over channel of this type hit
C
 1       CONTINUE
C
C  End loop over channel types
C
 999  CONTINUE
C
      RETURN
      END
