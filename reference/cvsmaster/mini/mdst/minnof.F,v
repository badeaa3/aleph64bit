head	1.1;
branch	1.1.1;
access;
symbols
	mini202_9:1.1.1.1
	mini202_8:1.1.1.1
	mini202_7:1.1.1.1
	mini202_6:1.1.1.1
	mini202_5:1.1.1.1
	mini202_4:1.1.1.1
	mini202_2:1.1.1.1
	mini202_1:1.1.1.1
	mini202:1.1.1.1
	mini201_1:1.1.1.1
	mini104_1:1.1.1.1
	mini103_1:1.1.1.1
	mini102_7:1.1.1.1
	mini102_5:1.1.1.1
	mini102_4:1.1.1.1
	mini102_3:1.1.1.1
	mini102_2:1.1.1.1
	mini102_1:1.1.1.1
	mini102:1.1.1.1
	flr:1.1.1;
locks; strict;
comment	@c @;


1.1
date	96.08.15.11.09.39;	author flr;	state Exp;
branches
	1.1.1.1;
next	;

1.1.1.1
date	96.08.15.11.09.40;	author flr;	state Exp;
branches;
next	;


desc
@@



1.1
log
@Initial revision
@
text
@      SUBROUTINE MINNOF(MODE)
C
CKEY MDST /INTERNAL
C-----------------------------------------------------------------------
C! Ensure no formats are written for POT banks.
C
C     Author: Stephen Haywood      28-Feb-91
C
C     Input  : MODE   = -1 to delete links to formats
C                     = +1 to restore links to formats
C
C     Called by MINOUT.
C
C     The banks whose formats will be dropped are listed on 'T' list,
C     which is prepared by MINOUT.
C
C     This requires changes to BOS system common.
C     The formats are disabled by zeroing their address. However, these
C     are restablished by garbage collections in the work space, and so
C     must be done just before the banks are written.
C-----------------------------------------------------------------------
#ifndef DOC
C
#include "bcs.h"
#include "mincom.h"
C
      COMMON / SYSBOS / NSYST,NAMES,NPRIM,IDNAM,IDPTR,IDFMT
      CHARACTER*4 NAME,NLIST
      DIMENSION KSAVE(MAXBNK)
      SAVE KREF,KSHFT,KSAVE
      DATA KREF,KSHFT,KSAVE / 2*0,MAXBNK*0 /
C
      IF (MODE.EQ.-1) CALL VZERO(KSAVE,MAXBNK)
C
C++   Look at location of format for a reference bank to detect
C++   shifts of formats resulting from garbage collection.
C++   RUNH is chosen since it does not share its root with other banks.
C
      IOFF = IW(IDNAM-2) + MOD(IABS(INTCHA('RUNH')),NPRIM) - 3
      IOFF = IW(IDPTR+IOFF)
      IF (MODE.EQ.-1) THEN
         KREF = IW(IDFMT+IOFF)
      ELSE
         KSHFT = IW(IDFMT+IOFF) - KREF
      ENDIF
C
C++   Loop over banks to be written out.
C
      DO 100 I=1,MAXBNK
         NAME = NLIST(IW,I,'T')
         IF (NAME.EQ.' ') GOTO 110
         IF (NAME(1:1).EQ.'D') GOTO 100
         IF (NAME(1:1).EQ.'d') GOTO 100
         INAME = INTCHA(NAME)
C
C++      Try to pick up pointer to bank format.
C++      The pointers are associated with the name and several may
C++      share the same root, therefore allow a small number of loops.
C++      If correct, IOFF should correspond to 'pos' in BOSIO table.
C
         IOFF = IW(IDNAM-2) + MOD(IABS(INAME),NPRIM) - 3
         LOOP = 0
   90    LOOP = LOOP + 1
            IF (LOOP.GT.6) GOTO 100
            IOFF = IW(IDPTR+IOFF)
            IF (IOFF.LE.0) GOTO 100
            IF (IW(IDNAM+IOFF).NE.INAME) GOTO 90
C
C++      Pick up pointer.
C
         IF (MODE.EQ.-1) THEN
            KFMT = IW(IDFMT+IOFF)
         ELSE
            IF (KSAVE(I).LE.0) GOTO 100
            KFMT = KSAVE(I) + KSHFT
         ENDIF
C
C++      Check we really have found correct format bank.
C
         IF (IW(KFMT-3).NE.INTCHA('+FMT')) GOTO 100
         IF (IW(KFMT+1).NE.INAME) GOTO 100
C
C++      Modify pointer: either zero it or restore it.
C
         IF (MODE.EQ.-1) THEN
            KSAVE(I) = KFMT
            IW(IDFMT+IOFF) = 0
         ELSE
            IW(IDFMT+IOFF) = KFMT
         ENDIF
  100 CONTINUE
  110 CONTINUE
C
      RETURN
      END
#endif
@


1.1.1.1
log
@1st release of mini under cvs
@
text
@@
