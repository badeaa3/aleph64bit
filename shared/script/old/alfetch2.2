# !/bin/csh
#
###############################################
#          Fetch the EDIR files from CERNVM
#   written by Joel Closier
###############################################
#
set echo on
echo ALFETCH runs the `date`
cd $ALEDIR

#  create the file .netrc
cat >$HOME/.netrc << END-OF-CAT
machine cernvm login anonymous
macdef init
cd alsoft.230
dir edir.filelist
quit

END-OF-CAT
#
#   get the list of the EDIR files to fetch
ftp_ultra cernvm > $ALEDIR/diff.txt
rm -f $HOME/.netrc
set lines=`wc -l $ALEDIR/diff.txt`
set line=$lines[1]
while ($line != 0)
    set com=`head -$line $ALEDIR/diff.txt | tail -1`
    if ($#com > 2) then
      if (($com[1] == "EDIR") && ($com[2] == "FILELIST")) then
        set FDATE = `date +%m/%d/%y`
        if ($com[7] != $FDATE) then
	   goto fetch
           exit (0)
        endif
      endif
    endif
    @ line--
    continue
end

exit 0


fetch:
#echo fetch
#   get the list of the EDIR files to fetch
if ( -r $ALEDIR/edir.filelist) mv $ALEDIR/edir.filelist $ALEDIR/edir.filelist.old
sfetch $ALEDIR/edir.filelist -t cernvm:edir.filelist -u alsoft.230
if !(-r $ALEDIR/edir.filelist.old) touch $ALEDIR/edir.filelist.old
if !(-r $ALEDIR/edir.filelist) then
  echo No update today because of problem
  exit 0
endif
diff $ALEDIR/edir.filelist.old $ALEDIR/edir.filelist > $ALEDIR/diff.txt

set lines=`wc -l $ALEDIR/diff.txt`
set line=$lines[1]

while ($line != 0)
    set com=`head -$line $ALEDIR/diff.txt | tail -1`
    set file=`echo $com[2].$com[3] | tr "[A-Z]" "[a-z]"`
    switch ($com[1])
      case <: 
              rm $ALEDIR/$file
              breaksw
      case >: 
              sfetch $ALEDIR/$file -t cernvm:$file -u pubxu.209
              breaksw
    endsw
    @ line--
    continue
end
rm $ALEDIR/diff.txt

#echo repare
ls $ALEDIR > $ALEDIR/edir.lst
awk '{printf "%s.%s\n",$1,$2}' $ALEDIR/edir.filelist | tr "[A-Z]" "[a-z]" > $ALEDIR/dif.txt
sort +1 -1 $ALEDIR/dif.txt > $ALEDIR/dif1.txt
diff $ALEDIR/edir.lst $ALEDIR/dif1.txt > $ALEDIR/dif2.txt

set lines=`wc -l $ALEDIR/dif2.txt`
set line=$lines[1]

while ($line != 0)
    set com=`head -$line $ALEDIR/dif2.txt | tail -1`
    if ($#com > 1) then
      set file=`echo $com[2] | tr "[A-Z]" "[a-z]"`
      switch ($com[1])
        case <:
              if (("$ALEDIR/$file" != "$ALEDIR/edir.lst") && ("$ALEDIR/$file" != "$ALEDIR/edir.filelist") && ("$ALEDIR/$file" != "$ALEDIR/lost+found") && ("$ALEDIR/$file" != "$ALEDIR/edir.filelist.old")) rm $ALEDIR/$file
              breaksw
        case >:
              sfetch $ALEDIR/$file -t cernvm:$file -u pubxu.209
              breaksw
        default: echo RIEN
               breaksw
      endsw
    endif
    @ line--
    continue
end
rm $ALEDIR/edir.lst $ALEDIR/dif.txt $ALEDIR/dif1.txt $ALEDIR/dif2.txt
exit 0




